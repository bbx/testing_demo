*** Settings ***
Suite Setup     start selenium server
Suite Teardown  stop selenium server
Test Setup      setup application
Test Teardown   close Browser
Library         Selenium Library
Library         OperatingSystem

*** Variables ***
${rc}  ${EMPTY}
${reservation_row}  ${EMPTY}

*** Test Cases ***
Login with default user
    Page Should Contain  You are not logged in
    Login
    Page Should Contain  Signed in as: First User
    Logout
    Page Should Contain  You are not logged in

Sign up
    Click Link  Sign up
    Input Text  user_email  test@testing.com
    Input password  user_password  password
    Input Password  user_password_confirmation  password
    Input text  user_name  Test User
    Click Button  Sign up
    Page Should Contain  Welcome! You have signed up successfully.
    Page Should Contain  Signed in as: Test User
    Logout

Edit account
    Login
    Click link  Edit account
    Click link  Cancel my account
    Confirm Action
    Page Should Contain  Bye! Your account was successfully cancelled. We hope to see you again soon.
    Page Should Contain  You are not logged in

Resource listing
    Login
    Page Should Contain Link  Resource1

Daily status
    Login
    open Resource1
    Page Should Contain  Reservations for today

Book and cancel a slot
    Login
    open Resource1
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'3:00 - 4:00')]/td[2]  Slot is Free
    Click button  xpath=//table/tbody/tr[contains (.,'3:00 - 4:00')]/td[2]/form/input[@value='reserve']
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'3:00 - 4:00')]/td[2]  Reserved for You
    Click button  xpath=//table/tbody/tr[contains (.,'3:00 - 4:00')]/td[2]/form/input[@value='cancel']
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'3:00 - 4:00')]/td[2]  Slot is Free

*** Keywords ***
Login
    Click Link  Login
    Input Text  user_email  user@example.com
    Input Password  user_password  please
    Click Button  Sign in

Logout
    Click Link  Logout

setup application
    ${rc}=  Run And Return Rc  rake -f ${CURDIR}${/}..${/}Rakefile db:reset
    Should Be Equal As Integers  ${rc}  0
    Open Browser  http://localhost:3000

open Resource1
    Click Link  Resource1

book a slot
    [Arguments]  ${slot_name}  ${is_free_text}  ${is_reserved_text}

