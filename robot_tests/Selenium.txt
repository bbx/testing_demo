*** Settings ***
Suite Setup     start selenium server
Suite Teardown  stop selenium server
Test Setup      Open Browser  http://localhost:3000
Test Teardown   close Browser
Library         Selenium Library  run_on_failure=Nothing  # #don't capture screenshots
Library         OperatingSystem

*** Variables ***
${rc}  ${EMPTY}
${default_is_free_text}  Slot is Free
${default_is_reserved_text}  Reserved for You
${output}  ${EMPTY}

*** Test Cases ***
Login with default user
    Page Should Contain  You are not logged in
    Login
    Page Should Contain  Signed in as: First User
    Logout
    Page Should Contain  You are not logged in

Sign up
    Click Link  Sign up
    Input Text  user_email  test@testing.com
    Input password  user_password  password
    Input Password  user_password_confirmation  password
    Input text  user_name  Test User
    Click Button  Sign up
    Page Should Contain  Welcome! You have signed up successfully.
    Page Should Contain  Signed in as: Test User
    Logout

Edit account
    Login  test@testing.com  password
    Click link  Edit account
    Click link  Cancel my account
    Confirm Action
    Page Should Contain  Bye! Your account was successfully cancelled. We hope to see you again soon.
    Page Should Contain  You are not logged in

Resource listing
    Login
    Page Should Contain Link  Resource1

Daily status
    Login
    open Resource
    Page Should Contain  Reservations for today

Book and cancel a slot
    Login
    open Resource
    book a slot  3:00 - 4:00
    cancel a slot  3:00 - 4:00

book a slot with second user
    Login  user2@example.com  please
    open Resource  # default
    book a slot  3:00 - 4:00
    Logout
    Login  # default #default
    open Resource  # default
    confirm booking  3:00 - 4:00  Reserved for: Second User

*** Keywords ***
Login
    [Arguments]  ${user_name}=user@example.com  ${password}=please
    Click Link  Login
    Input Text  user_email  ${user_name}
    Input Password  user_password  ${password}
    Click Button  Sign in

Logout
    Click Link  Logout

setup application
    ${rc}  ${output}=  Run And Return Rc and Output  ${CURDIR}${/}..${/}reset_db.sh
    Should not contain  ${output}  Failed
    Should Be Equal As Integers  ${rc}  0
    Open Browser  http://localhost:3000

open Resource
    [Arguments]  ${resource_name}=Resource1
    Click Link  ${resource_name}

book a slot
    [Arguments]  ${slot_name}  ${is_free_text}=${default_is_free_text}  ${is_reserved_text}=${default_is_reserved_text}
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]  ${is_free_text}
    Click button  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]/form/input[@value='reserve']
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]  ${is_reserved_text}

cancel a slot
    [Arguments]  ${slot_name}  ${is_free_text}=${default_is_free_text}  ${is_reserved_text}=${default_is_reserved_text}
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]  ${is_reserved_text}
    Click button  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]/form/input[@value='cancel']
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]  ${is_free_text}

confirm booking
    [Arguments]  ${slot_name}  ${is_reserved_text}=${default_is_reserved_text}
    Element Should Contain  xpath=//table/tbody/tr[contains (.,'${slot_name}')]/td[2]  ${is_reserved_text}

